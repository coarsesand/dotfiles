# Prompt based on one seen in jekor's Haskell from Scratch video series

function prompt_jekor_help {
  cat <<EOH
Prompt based on the one seen in jekor's Haskell from Scratch video series.
Includes support for virtualenv. Requires a unicode capable terminal.
EOH
}

function prompt_jekor_preview {
  prompt_preview_theme jekor
}

function prompt_jekor_precmd {
  # Get virtualenv info
  if (( $+functions[python-info] )); then
    python-info
  fi

  # Get git info
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_jekor_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_jekor_precmd

  zstyle ':prezto:module:editor:info:completing' format '%F{yellow}...%f'
  zstyle ':prezto:module:python:info:virtualenv' format '%F{magenta}(%v) '
  zstyle ':prezto:module:git:info:branch' format '%F{cyan}[%b]'
  zstyle ':prezto:module:git:info:keys' format 'prompt' '%b '

  # Define prompts
  PROMPT='%(?.%F{green}✓%f.%F{red}✗ %?%f) %F{green}%n %F{blue}$(pwd_trunc.py) $git_info[prompt]$python_info[virtualenv]λ%f '

  RPROMPT=''
}

prompt_jekor_setup "$@"
