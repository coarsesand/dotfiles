#
# A prompt based on Steve Losh's extravagant prompt. Uses git-info for
# repository information.
#

function prompt_csand_help {
  cat <<EOH
This prompt is designed for use on 256 colour terminals.
EOH
}

function prompt_csand_preview {
  prompt_preview_theme csand
}

function prompt_csand_precmd {
  # Get virtual environment information
  if (( $+functions[python-info] ))
  then
    python-info
  fi

  # Get Git repository information
  if (( $+functions[git-info] ))
  then
    git-info
  fi
}

function prompt_csand_preexec {
  # Emit a blank line before executing the command unless the command is
  # known to not print anything. Unfortunately, I'm not sure how to compensate
  # for AUTO_CD.
  cmd=(${=1})
  case $cmd[1] in
    'cd') ;;
    *) echo '';;
  esac
}

function prompt_csand_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions
  autoload -Uz add-zsh-hook

  # Add hooks for precmd and preexec
  add-zsh-hook precmd  prompt_csand_precmd
  add-zsh-hook preexec prompt_csand_preexec

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'

  # git info format
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format 'branch %F{4}%b%f'
  zstyle ':prezto:module:git:info:modified' format '%F{3}!%f'
  zstyle ':prezto:module:git:info:untracked' format '%F{3}?%f'
  zstyle ':prezto:module:git:info:added' format '%F{10}+%f'
  zstyle ':prezto:module:git:info:ahead' format ' up %F{12}%A%f'
  zstyle ':prezto:module:git:info:behind' format ' down %F{11}%A%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt'  ' %b%m%u%a%A%B' \
    'rprompt' ''

  # virtualenv info format
  zstyle ':prezto:module:python:info:virtualenv' format ' venv %F{77}%v%f'

  # Define prompts
  PROMPT='
  %F{11}%n%f at %F{1}%m%f in %F{5}%~%f${git_info:+${(e)git_info[prompt]}}%f${python_info[virtualenv]}
  %F{15}â‡’%f  '

  RPROMPT=''
}

prompt_csand_setup "$@"
